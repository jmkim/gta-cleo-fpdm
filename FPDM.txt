{$CLEO .cs}

0000: NOP
03A4: name_thread 'FPDM'

1@ = 0.0 // x offset (float)
2@ = -0.2 // y offset (float)
3@ = 0.666 // z offset (float)
//4@ =	// x rotation (float)
//5@ =	// y rotation (float)
//6@ =	// z rotation (float)
7@ = 0.0 // rotation (float)
8@ = 2 // switch style (integer) (1 - ground, 2 - actor)

10@ = 0 // default view (0 - off, 1 - on, 2 - using last status)
11@ = 0 // remove front window (0 - no, 1 - yes)

12@ = 49 // toggle, 1
13@ = 81 // left, Q
14@ = 69 // right, E
// http://www.gta-modding.com/san_andreas/tutorials/virtual_key_codes.html

0AF0: 10@ = get_int_from_ini_file "CLEO\FPDM.INI" section "GENERAL" key "DEFAULT_VIEW"
0AF0: 11@ = get_int_from_ini_file "CLEO\FPDM.INI" section "GENERAL" key "REMOVE_WINDOW"
0AF0: 12@ = get_int_from_ini_file "CLEO\FPDM.INI" section "BIND_KEY" key "TOGGLE"
0AF0: 13@ = get_int_from_ini_file "CLEO\FPDM.INI" section "BIND_KEY" key "LEFT"
0AF0: 14@ = get_int_from_ini_file "CLEO\FPDM.INI" section "BIND_KEY" key "RIGHT"

:FPDM_WALK
while true
	wait 0
	if
		00DF: actor $PLAYER_ACTOR driving // when player begins driving...
	then
		if
			0039: 10@ == 1
		then
			jump @FPDM_RIDE
		end
		if and
			0039: 10@ == 2
			0039: 20@ == 1
		then
			jump @FPDM_RIDE
		end
		while true
			wait 0
			if
				0AB0: key_pressed 12@ // toggle - on
			then
				jump @FPDM_RIDE
			end
		end
	end
end

:FPDM_RIDE
wait 0
20@ = 1
if and
	847A: not actor $PLAYER_ACTOR driving_bike
	84A7: not actor $PLAYER_ACTOR driving_boat
	84C8: not actor $PLAYER_ACTOR driving_flying_vehicle
then
	03C0: 21@ = actor $PLAYER_ACTOR car
	067F: set_car 21@ lights 2
	0697: set_car 21@ remove_component 4 visible_effect_flag 0
end
067C: put_camera_on_actor $PLAYER_ACTOR offset 1@ 2@ 3@ rotation 0.0 180.0 -10.0 tilt 7@ switchstyle 8@
wait 300
jump @FPDM_FRONT

:FPDM_FRONT
while true
	wait 0
	067C: put_camera_on_actor $PLAYER_ACTOR offset 1@ 2@ 3@ rotation 0.0 180.0 -10.0 tilt 7@ switchstyle 8@
	if
		80DF: not actor $PLAYER_ACTOR driving // When player stops driving...
	then
		jump @FPDM_RESTORE
	end
	wait 0
	if
		0AB0: key_pressed 12@ // toggle - off
	then
		20@ = 2
		jump @FPDM_RESTORE
	end
	wait 0
	if
		0AB0: key_pressed 13@ // left
	then
		jump @FPDM_LEFT
	end
	wait 0
	if
		0AB0: key_pressed 14@ // right
	then
		jump @FPDM_RIGHT
	end
end

:FPDM_RESTORE
02EB: restore_camera_with_jumpcut
if and
	056E: car 21@ defined
	847A: not actor $PLAYER_ACTOR driving_bike
	84A7: not actor $PLAYER_ACTOR driving_boat
	84C8: not actor $PLAYER_ACTOR driving_flying_vehicle
then
	067F: set_car 21@ lights 0
	0698: set_car 21@ repair_componentB 4
end
wait 300
jump @FPDM_WALK

:FPDM_LEFT
067C: put_camera_on_actor $PLAYER_ACTOR offset 1@ 2@ 3@ rotation -180.0 180.0 -35.0 tilt 7@ switchstyle 8@
while true
	wait 0
	if
		8AB0: not key_pressed 13@ // left
	then
		jump @FPDM_FRONT
	end
	wait 0
	if
		0AB0: key_pressed 14@ // right
	then
		jump @FPDM_BEHIND
	end
end

:FPDM_RIGHT
067C: put_camera_on_actor $PLAYER_ACTOR offset 1@ 2@ 3@ rotation 180.0 135.0 -35.0 tilt 7@ switchstyle 8@
while true
	wait 0
	if
		8AB0: not key_pressed 14@ // right
	then
		jump @FPDM_FRONT
	end
	wait 0
	if
		0AB0: key_pressed 13@ // left
	then
		jump @FPDM_BEHIND
	end
end

:FPDM_BEHIND
2@ -= 0.2
067C: put_camera_on_actor $PLAYER_ACTOR offset 1@ 2@ 3@ rotation 0.0 -180.0 0.0 tilt 7@ switchstyle 8@
2@ += 0.2
while true
	wait 0
	if
		8AB0: not key_pressed 14@ // right
	then
		jump @FPDM_LEFT
	end
	wait 0
	if
		8AB0: not key_pressed 13@ // left
	then
		jump @FPDM_RIGHT
	end
end
